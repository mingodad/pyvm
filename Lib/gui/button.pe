__autosem__
from widget import widget
from label import label

# currently, backslashes in the button text are used to underline
# hotkeys. But *not* actually do hotkey-on-keypress. that should
# be done by the main key control function.

class button (label)
{
	method Init (t, **kw)
	{
		try f = kw ['font']
		except f = kw ['font'] = $A.buttonfont

		if ($tabgroup = kw.pop ("tabgroup", False))
			$enter_tabgroup ()

		if ('w' not in kw)
			if ("alignto" in kw)
				kw ["w"] = kw.pop ("alignto").w
			else kw ['w'] = f.isizeof (" " + t + " ") + 10

		if ('h' not in kw)
			kw ['h'] = f.size ()[1] + 2

		if ('Action' in kw) $Action = kw.pop ('Action')
		$maxw, $maxh = kw ['w'], kw ['h']
		label.Init (self, t, **kw)
	}

	button_init = Init

	method active_color ()
		return $enabled ? $A.color_txt : $A.color_shaded;

	method onKey (k)
		if (k in ' \n') {
			$onClick (0, 0);
			return 1;
		} else if (k in ("\t", "DOWN"))
			return $nextKeyWidget ()
		else if (k == "UP")
			return $prevKeyWidget ()

	method DrawText ()
	{
		t = $t;
		while ($font.sizeof (t) > $w and t)
			t = t [:-1];
		fg = $enabled ? $A.color_txt : $A.color_shaded;
		$draw_ulstring (t, $tx, $ty, fg, $font, $wm.color_button_bg, maxw=$w);
	}

	method Draw ()
	{
		$draw_blank ($wm.color_button_bg)
		$outline ('raised', funky=0)
		$DrawText ()
	}

	method onClick (x, y)
	{
		if (not $enabled)
			return

		$outline ('sunken')
		$A.flush ()
		t0 = now ()
		$Action ()
		if ((dt = now () - t0) < 0.1)
			$wm.sleep (0.1 - dt)
		$Draw ()
		$A.flush ()
	}

	method Click ()
		$onClick (0, 0)

	Action = void
}

class ImageButton (widget)
{
	elastic = False
	Action = void

	method Init (img, **kw)
	{
		if (img -> str)
			img = @graphics.ImageFile (img).read_image ()
		$img = img.convert_bpp ($wm.bpp)
		if ('Action' in kw)
			$Action = kw.pop ('Action')
		kw ["w"] = $img.w
		kw ["h"] = $img.h
		$widget_init (**kw)
	}

	method Draw ()
		$show_image ($img, 0, 0)

	method onClick (x, y)
	{
		if (!$enabled)
			return
		$show_image ($img, 1, 1, w=$w-1, h=$h-1)
		$A.flush ()
		t0 = now ()
		$Action ()
		if ((dt = now () - t0) < 0.1)
			$wm.sleep (0.1 - dt)
		$Draw ()
	}
}

class ImageToggleButton (widget)
{
	elastic = False
	onToggle = void

	method Init (img1, img2, $state=True, **kw)
	{
		if (img1 -> str)
			img1 = @graphics.ImageFile (img1).read_image ()
		$img1 = img1.convert_bpp ($wm.bpp)
		if (img2 -> str)
			img2 = @graphics.ImageFile (img2).read_image ()
		$img2 = img2.convert_bpp ($wm.bpp)
		if ('onToggle' in kw)
			$onToggle = kw.pop ('onToggle')
		kw ["w"] = img1.w
		kw ["h"] = img1.h
		$widget_init (**kw)
	}

	method Draw ()
		$show_image ($state ? $img1 : $img2, 0, 0)

	method onClick (x, y)
	{
		if (!$enabled)
			return
		$toggle ()
	}

	method toggle ()
	{
		if ($onToggle ($state))
			$state = not $state
		$Draw ()
	}

}
